%description:
Testing EtherMac communications 
    10Mbps half duplex
    10Mbps full duplex
   100Mbps half duplex
   100Mbps full duplex
  1000Mbps full duplex
%#--------------------------------------------------------------------------------------------------------------
%testprog: opp_run
%#--------------------------------------------------------------------------------------------------------------
%file: test.ned

import ned.DatarateChannel;
import inet.nodes.ethernet.EtherHost;

//
// Sample Ethernet LAN: two hosts directly connected to each other
// via twisted pair.
//
module TwoHosts
{
    parameters:
        double speed @unit(bps) = default(100Mbps);
        bool isduplex = default(true);
    types:
        channel C extends DatarateChannel
        {
            delay = 0s;
            datarate = speed;
        }
    submodules:
        hostA: EtherHost {
            parameters:
                @display("p=70,70");
        }
        hostB: EtherHost {
            parameters:
                @display("p=190,70");
        }
    connections:
        hostA.ethg <--> C <--> hostB.ethg;
}

network SpeedTest
{
    submodules:
        Hosts1000F: TwoHosts {
            parameters:
                speed = 1000Mbps;
                isduplex = true;
                @display("p=70,70");
        }
        Hosts100F: TwoHosts {
            parameters:
                speed = 100Mbps;
                isduplex = true;
                @display("p=70,140");
        }
        Hosts100H: TwoHosts {
            parameters:
                speed = 100Mbps;
                isduplex = false;
                @display("p=190,140");
        }
        Hosts10F: TwoHosts {
            parameters:
                speed = 10Mbps;
                isduplex = true;
                @display("p=70,210");
        }
        Hosts10H: TwoHosts {
            parameters:
                speed = 10Mbps;
                isduplex = false;
                @display("p=190,210");
        }
}
%#--------------------------------------------------------------------------------------------------------------
%inifile: omnetpp.ini
#
# To try: ./LANs -f TwoHosts.ini
#

[General]
sim-time-limit = 10s

tkenv-plugin-path = ../../../etc/plugins
**.vector-recording = true
#record-eventlog = true

network = SpeedTest

*.Hosts1000F.hostA.cli.destAddress = "Hosts1000F.hostB"
*.Hosts1000F.hostB.cli.destAddress = "Hosts1000F.hostA"

*.Hosts100F.hostA.cli.destAddress = "Hosts100F.hostB"
*.Hosts100F.hostB.cli.destAddress = "Hosts100F.hostA"

*.Hosts100H.hostA.cli.destAddress = "Hosts100H.hostB"
*.Hosts100H.hostB.cli.destAddress = "Hosts100H.hostA"

*.Hosts10F.hostA.cli.destAddress = "Hosts10F.hostB"
*.Hosts10F.hostB.cli.destAddress = "Hosts10F.hostA"

*.Hosts10H.hostA.cli.destAddress = "Hosts10H.hostB"
*.Hosts10H.hostB.cli.destAddress = "Hosts10H.hostA"


**.cli.reqLength = 1250B       # 10.000 bit
**.cli.respLength = 1250B      # 10.000 bit
**.hostA.cli.startTime = 0s
**.hostB.cli.startTime = 99999s    # out of sim-time-limit
**.hostB.cli.waitTime  = 99999s    # out of sim-time-limit

*.Hosts*F.*.mac.duplexEnabled = true
*.Hosts*H.*.mac.duplexEnabled = false

*.Hosts1000F.hostA.cli.waitTime = 0.0105ms        # 10.000 / speed [ / 2 when halfduplex]
*.Hosts100F.hostA.cli.waitTime = 0.103ms        # 10.000 / speed [ / 2 when halfduplex]
*.Hosts100H.hostA.cli.waitTime = 0.208ms        # 10.000 / speed [ / 2 when halfduplex]
*.Hosts10F.hostA.cli.waitTime = 1ms        # 10.000 / speed [ / 2 when halfduplex]
*.Hosts10H.hostA.cli.waitTime = 2ms        # 10.000 / speed [ / 2 when halfduplex]


**.mac.address = "auto"

# Check: "rx channel idle (%)" <= 5.0
# Check: "rx channel utilization (%)" >= 95.0
%#--------------------------------------------------------------------------------------------------------------
%postprocess-script: check.r
#!/usr/bin/env Rscript

options(echo=FALSE)
options(width=160)
library("omnetpp", warn.conflicts=FALSE)

#TEST parameters
scafile <- 'results/General-0.sca'
linecount <- 10
idlelimit <- 5.0
usedlimit <- 95.0

# begin TEST:

idle <- loadDataset(add(type='scalar', files=scafile, select='name("rx channel idle *")'))
used <- loadDataset(add(type='scalar', files=scafile, select='name("rx channel utilization *")'))

cat("\nOMNETPP TEST RESULT: ")

if(length(idle$scalars$value) == linecount && max(idle$scalars$value) <= idlelimit)
{
    cat("IDLE OK\n")
} else {
    cat("IDLE BAD:\n")
    print(idle$scalars[idle$scalars$value > idlelimit,])
}

cat("\nOMNETPP TEST RESULT: ")

if(length(used$scalars$value) == linecount && min(used$scalars$value) >= usedlimit)
{
    cat("USED OK\n")
} else {
    cat("USED BAD:\n")
    print(used$scalars[used$scalars$value < usedlimit,])
}

cat("\n")
%#--------------------------------------------------------------------------------------------------------------
%contains: check.r.out
Loaded results/General-0.sca
Loaded results/General-0.sca

OMNETPP TEST RESULT: IDLE OK

OMNETPP TEST RESULT: USED OK

%#--------------------------------------------------------------------------------------------------------------
